grammar org.eclipse.smarthome.model.Items

// disabled due to conflicts between the custom ID rule and the common INT rule
// with org.eclipse.xtext.common.Terminals

hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate items "http://www.eclipse.org/smarthome/model/Items"

ItemModel :
	{ItemModel} (items+=ModelItem)*
;

ModelItem :
	(ModelNormalItem | ModelGroupItem) name=ID
	(label=STRING)?
	('<' icon=(ID|STRING) '>')?
	('(' groups+=ID (',' groups+=ID)* ')')? 
	('[' tags+=(ID|STRING) (',' tags+=(ID|STRING))* ']')?
	('{' bindings+=ModelBinding (',' bindings+=ModelBinding)* '}')? 
;

ModelGroupItem :
	{ModelGroupItem} 'Group' (':' type=ModelItemType ( ':' function=ModelGroupFunction ('(' args+=(ID|STRING) (',' args+=(ID|STRING))* ')')?)?)?
;

enum ModelGroupFunction :
	EQUAL='EQUAL' | AND='AND' | OR='OR' | NAND='NAND' | NOR='NOR' | AVG='AVG' | SUM='SUM' | MAX='MAX' | MIN='MIN' | COUNT='COUNT'
;

ModelNormalItem :
	type=ModelItemType (':' function=ModelDimension)?
;

enum ModelDimension : 
    DIMENSIONLESS='DIMENSIONLESS' | ACCELERATION='ACCELERATION' | AMOUNT_OF_SUBSTANCE='AMOUNT_OF_SUBSTANCE' | ANGLE='ANGLE' | AREA='AREA' | 
    CATALIYTIC_ACTIVITY='CATALIYTIC_ACTIVITY' | ELECTRIC_CAPACITANCE='ELECTRIC_CAPACITANCE' |
    ELECTRIC_CHARGE='ELECTRIC_CHARGE' | ELECTRIC_CONDUCTANCE='ELECTRIC_CONDUCTANCE' | ELECTRIC_CURRENT='ELECTRIC_CURRENT' |
    ELECTRIC_INDUCTANCE='ELECTRIC_INDUCTANCE' | ELECTRIC_POTENTIAL='ELECTRIC_POTENTIAL' | ELECTRIC_RESISTANCE='ELECTRIC_RESISTANCE' |
    ENERGY='ENERGY' | FORCE='FORCE' | FREQUENCY='FREQUENCY' | ILLUMINANCE='ILLUMINANCE' | LENGTH='LENGTH' | LUMINOUS_FLUX='LUMINOUS_FLUX' |
    LUMINOUS_INTENSITY='LUMINOUS_INTENSITY' | MAGNETIC_FLUX='MAGNETIC_FLUX' | MAGNETIC_FLUX_DENSITY='MAGNETIC_FLUX_DENSITY' |
    MASS='MASS' | POWER='POWER' | PRESSURE='PRESSURE' | RADIATION_DOSE_ABSORBED='RADIATION_DOSE_ABSORBED' | 
    RADIATION_DOSE_EFFECTIVE='RADIATION_DOSE_EFFECTIVE' | RADIO_ACTVITY='RADIO_ACTVITY' | SOLID_ANGLE='SOLID_ANGLE' | SPEED='SPEED' |
    TEMPERATURE='TEMPERATURE' | TIME='TIME' | VOLUME='VOLUME'
;

ModelItemType :
	'Switch' | 'Rollershutter' | 'Number' | 'String' | 'Dimmer' | 'Contact' | 'DateTime' | 'Color' | 'Player' | 'Location' | 'Call' | ID
;

ModelBinding:
	type=ID '=' configuration=STRING   
	('['
        properties+=ModelProperty? (',' properties+=ModelProperty)*
    ']')?
;

ModelProperty:
    key=ID '=' value=ValueType
;

ValueType returns ecore::EJavaObject:
    STRING | NUMBER | BOOLEAN
;

BOOLEAN returns ecore::EBoolean: 
    'true' | 'false'
;

NUMBER returns ecore::EBigDecimal:
    ID ('.' ID )?
;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_'|'0'..'9') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
